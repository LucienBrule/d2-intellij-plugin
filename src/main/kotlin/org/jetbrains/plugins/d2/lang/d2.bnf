{
		parserClass="org.jetbrains.plugins.d2.lang.D2Parser"

		elementTypeHolderClass="org.jetbrains.plugins.d2.lang.D2ElementTypes"
		elementTypeClass="org.jetbrains.plugins.d2.lang.D2ElementType"
		elementTypeFactory="org.jetbrains.plugins.d2.lang.D2ElementTypeFactory.element"

		generatePsi=false

		tokenTypeClass="com.intellij.psi.tree.IElementType"
		tokenTypeFactory="org.jetbrains.plugins.d2.lang.D2ElementTypeFactory.token"

		psiTreeUtilClass="org.jetbrains.plugins.d2.lang.D2PsiTreeUtil"

		psiClassPrefix="D2"
		psiImplClassSuffix="Impl"
		psiPackage="org.jetbrains.plugins.d2.lang.psi"
		psiImplPackage="org.jetbrains.plugins.d2.lang.psi.impl"
}

File ::= !<<eof>> (ShapeDeclaration | ShapeIdWithProperty | ShapeConnection | ShapeId | COMMENT | SEMICOLON)*

// same as for file level, but Property is allowed without ShapeIdChain
BlockDefinition ::= LBRACE (ShapeDeclaration | ((ShapeIdChain DOT)? Property) | ShapeConnection | ShapeId | COMMENT | SEMICOLON)* RBRACE {
		pin=1
}

private ShapeIdChain ::= ShapeId (DOT ShapeId)*

// declaration means `id + label` (not implicit shape via just ID)
ShapeDeclaration ::= ShapeIdChain COLON (ShapeLabel | BlockString)? BlockDefinition?

ShapeConnection ::= ShapeIdChain (Connector ShapeId (DOT ShapeId)*)+ (COLON (ShapeLabel | BlockString))? BlockDefinition?

// logs.style.strokes: "#f4a261"
private ShapeIdWithProperty ::= ShapeIdChain DOT Property

// bad tree structure: ci.deploys->clouds<-a.sub:test
//private ShapeDefinitions ::= ShapeId (ShapeConnection | SubShapeDefinition)* ShapeExtras? {
//  pin=1
//}

//private SubShapeDefinition ::= DOT ShapeId ShapeExtras?

ShapeId ::= ID | STRING

ShapeLabel ::= UNQUOTED_STRING | STRING

Connector ::= ARROW | REVERSE_ARROW | DOUBLE_ARROW | DOUBLE_HYPHEN_ARROW

BlockString ::= BLOCK_STRING_OPEN BLOCK_STRING_LANG? BLOCK_STRING_BODY BLOCK_STRING_CLOSE

UnquotedStringValue ::= UNQUOTED_STRING
StringValue ::= STRING
ColorValue ::= COLOR
OtherValue ::= AttributeValue | BlockString

// todo: handle RESERVED_KEYWORD_HOLDERS in the same way as style (do not allow arbitrary ID)
PropertyKey ::= SIMPLE_RESERVED_KEYWORDS | (RESERVED_KEYWORD_HOLDERS DOT ID) | (STYLE_KEYWORD DOT STYLE_KEYWORDS)
private PropertyValue ::= UnquotedStringValue | StringValue | ColorValue | OtherValue

Property ::= PropertyKey COLON PropertyValue

private AttributeValue ::= STRING | ID | INT | FLOAT | TRUE | FALSE | DOT | COLOR